Wlpp+ Language Specification
============================

Index
-----
1. Lexical Syntax
   - Reserved Keywords
   - Reserved Symbols
2. Context-free Syntax
   - Terminal Symbols
   - Non-terminal Symbols
   - Start Symbol
   - Production Rules


1. Lexical Syntax
-----------------

    **********************************************************************
    * Reserved keywords:            * Reserved Symbols:                  *
    *                               *                                    *
    * wain                          * (    ,    &                        *
    * int                           * )    <    ;                        *
    * char                          * {    >                             *
    * if                            * }    *                             *
    * else                          * [    /                             *
    * while                         * ]    +                             *
    * return                        * =    -                             *
    * NULL                          * !    %                             *
    **********************************************************************

2. Context-free Syntax
----------------------

Terminal Symbols:

    Symbol: Definition/Lexeme

    WAIN:   wain
    INTK:   int
    CHARK:  char
    IF:     if
    ELSE:   else
    WHILE:  while
    RETURN: return
    NUL:    NULL
    ID:     A string consisting of a letter (in the range of a-z or A-Z)
            followed by zero or more letters and digits (in the rage 0 - 9).
            Valid ID cannot be any of the reserved keywords above.
    INT:    An integer number (in the rage of 0 - 2^32)
    CHAR:   An ASCII character
    COMMENT://
    LPAREN: (
    RPAREN: )
    LBRACE: {
    RBRACE: }
    LBRACK: [
    RBRACK: ]
    BECOMES:=
    EQ:     ==
    NE:     !=
    LT:     <
    GT:     >
    LE:     <=
    GE:     >=
    PLUS:   +
    MINUS:  -
    STAR:   *
    SLASH:  /
    PCT:    %
    AMP:    &
    AND:    &&
    COMMA:  ,
    SEMI:   ;

Non-terminal Symbols:

    Start
    proceduresAll
    procedures
    procedureWain
    procedure
    type
    dcls
    dcl
    statements
    statement
    tests
    test
    expr
    term
    factor
    lvalue

Start Symbol:

    Start

Production Rules:

    Start -> bof proceduresAll eof
    proceduresAll -> procedureWain procedures
    procedures -> procedures procedure
    procedures -> Nothing
    procedureWain -> INTK WAIN LPAREN dcl COMMA dcl RPAREN
                     LBRACE
                     dcls statements RETURN expr SEMI
                     RBRACE
    procedure -> type ID LPAREN dcl COMMA dcl RPAREN
                 LBRACE
                 dcls statements RETURN expr SEMI
                 RBRACE
    type -> INTK
    type -> INTK STAR
    type -> CHARK
    type -> CHARK STAR
    dcls -> dcls dcl BECOMES expr SEMI
    dcls -> dcls dcl SEMI
    dcls -> Nothing
    dcl -> type ID
    statements -> statements statement
    statements -> Nothing
    statement -> lvalue BECOMES expr SEMI
    statement -> IF LPAREN tests RPAREN
                  LBRACE statements RBRACE
    statement -> IF LPAREN tests RPAREN
                  LBRACE statements RBRACE
                  ELSE LBRACE statements RBRACE
    statement -> WHILE LPAREN tests RPAREN
                  LBRACE statements RBRACE
    tests -> tests AND test
    tests -> test
    test -> expr EQ expr
    test -> expr NE expr
    test -> expr LT expr
    test -> expr LE expr
    test -> expr GE expr
    test -> expr GT expr
    expr -> term
    expr -> expr PLUS term
    expr -> expr MINUS term
    term -> factor
    term -> term STAR factor
    term -> term SLASH factor
    term -> term PCT factor
    factor -> ID
    factor -> INT
    factor -> CHAR
    factor -> NUL
    factor -> LPAREN expr RPAREN
    factor -> AMP lvalue
    factor -> STAR factor
    lvalue -> ID
    lvalue -> STAR factor
    lvalue -> LPAREN lvalue RPAREN
